####################
## Hybrid Assembly Pipeline 1:
## Canu + Nanopolish + Circlator + Pilon
####################

rule _all_draft1_nofast5:
 input:
  expand(config['project_dir'] + '/reports/04_canu/{barcode}/asm.aln.tsv', barcode=BARCODES),
  expand(config['project_dir'] + '/06_circlator/.{barcode}_done_circlator', barcode=BARCODES)

#### Long Reads Assembler: Canu
rule canu_asm:
 input:
  reads = config['project_dir'] + '/03_subsampled_reads/{barcode}/reads.fastq.gz'
 output:
  config['project_dir'] + '/04_canu/{barcode}/canu.contigs.fasta',
  config['project_dir'] + '/04_canu/{barcode}/canu.correctedReads.fasta.gz'
 params:
  outdir = config['project_dir'] + '/04_canu/{barcode}'
 threads: 16
 shell:
  """
  LD_LIBRARY_PATH="$CONDA_PREFIX/lib64" canu \
   -p canu -d {params.outdir} genomeSize={config[canu_genome_size]} \
   -nanopore-raw {input.reads} correctedErrorRate=0.16 useGrid=false \
   maxMemory=20G maxThreads={threads} gnuplotTested=true
  """

rule assess_canu:
 input:
  genome = config['project_dir'] + '/' + config['genome_fp'],
  contigs = config['project_dir'] + '/04_canu/{barcode}/canu.contigs.fasta',
  python_chop_fp = config['basecalling_cmp_fp'] + '/chop_up_assembly.py',
  python_ident_fp = config['basecalling_cmp_fp'] + '/read_length_identity.py'
 output:
  asm_pieces = config['project_dir'] + '/04_canu/{barcode}/asm_pieces.fasta',
  asm_aln = config['project_dir'] + '/04_canu/{barcode}/asm.aln.paf',
  asm_table = config['project_dir'] + '/reports/04_canu/{barcode}/asm.aln.tsv'
 threads: 4
 shell:
  """
  python {input.python_chop_fp} {input.contigs} 10000 > {output.asm_pieces}
  minimap2 -k12 -t {threads} -c {input.genome} {output.asm_pieces} > {output.asm_aln}
  python {input.python_ident_fp} {output.asm_pieces} {output.asm_aln} > {output.asm_table}
  """

#### Circlator: trim overhangs and circularize contigs
rule run_circlator:
 input:
  contigs = config['project_dir'] + '/04_canu/{barcode}/canu.contigs.fasta',
  correctedReads = config['project_dir'] + '/04_canu/{barcode}/canu.correctedReads.fasta.gz'
 output:
  config['project_dir'] + '/06_circlator/.{barcode}_done_circlator',
 params:
  outdir = config['project_dir'] + '/06_circlator/{barcode}',
  contigs = config['project_dir'] + '/06_circlator/{barcode}/06.fixstart.fasta'
 threads: 4
 shell:
  """
  circlator all --merge_min_id 85 --merge_breaklen 1000 --threads {threads} --verbose \
  --assembler canu --data_type nanopore-corrected --bwa_opts "-x ont2d" \
   {input.contigs} {input.correctedReads} {params.outdir}
  touch {output}
  """
